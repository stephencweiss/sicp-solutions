Exercise 1.7: The good-enough? test used in computing square roots will not be very effective for finding the square roots of very small numbers. Also, in real computers, arithmetic operations are almost always performed with limited precision. This makes our test inadequate for very large numbers. Explain these statements, with examples showing how the test fails for small and large numbers. An alternative strategy for implementing good-enough? is to watch how guess changes from one iteration to the next and to stop when the change is a very small fraction of the guess. Design a square-root procedure that uses this kind of end test. Does this work better for small and large numbers?

---

A fixed threshold can run into two separate problems:
1. The threshold will be too big - this is most easily seen with small numbers and (relatively) large thresholds. In this case, resulting answer will not be "close" to the "actual" answer.
2. The threshold will be too small - most easily seen with very large numbers. This is the result of a limited number of bits. Because numbers will be rounded to make space, if the the threshold is too small, the procedure can wind up in an infinite loop as the guesses will never be within the threshold.

Possible implementations of `good-enough?` that work better:

```scheme
(define (good-enough? guess x)
  (< (abs (- (improve guess x) guess))
     (* guess .001)
  )
)
```
In this way, we look at the change in the guess relative to itself - so that if it's very large, we're looking for a relatively large change, but the acceptable change size shrinks as the guess itself shrinks.

Another way is to allow the limitations of the language to work to our advantage and see when the guess stops changing:

```scheme
(define (good-enough? guess x)
  (= (improve guess x) guess))
```

Seeing it in practice:

```scheme:title=sqrt-iter
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (average x y)
  (/ (+ x y) 2))

(define (improve guess x)
  (average guess (/ x guess)))

(define (sqrt x)
  (sqrt-iter 1.0 x))

(define (square x)
  (* x x))

(sqrt .00000001)                          ; 0.03125010656242753
```

```diff-scheme:title=sqrt-iter-relative
(define (good-enough? guess x)
-   (< (abs (- (square guess) x)) 0.001))
+   (< (abs (- (improve guess x) guess))
+      (* guess .001)
+   )
+ )
```

```scheme
(sqrt .00000001)                          ; 0.00010000040611237676
(sqrt 4)                                  ; 2.000609756097561
```

```diff-scheme:title=sqrt-iter-absolute
(define (good-enough? guess x)
-   (< (abs (- (square guess) x)) 0.001))
+   (= (improve guess x) guess)))
```

```scheme
(sqrt .00000001)                          ; 9.999999999999999e-5
(sqrt 4)                                  ; 2.0
```
